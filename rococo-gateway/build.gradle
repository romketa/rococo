plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.cloud.tools.jib' version '3.4.1'
}

group = 'guru.qa.rococo'
version = '1.0.0'

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

dependencyManagement {
	imports {
		mavenBom 'org.springframework.grpc:spring-grpc-dependencies:0.4.0-SNAPSHOT'
	}
}


dependencies {
	implementation project(':rococo-grpc-common')

	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.grpc:spring-grpc-client-spring-boot-starter'
	implementation 'org.springframework.data:spring-data-commons'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'io.grpc:grpc-netty-shaded'
	modules {
		module("io.grpc:grpc-netty") {
			replacedBy("io.grpc:grpc-netty-shaded", "Use Netty shaded instead of regular Netty")
		}
	}
	implementation 'org.springframework.experimental.boot:spring-boot-testjars-maven:0.0.3'
	implementation('org.springframework.boot:spring-boot-starter-web-services') {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
	}
	implementation "org.springframework.kafka:spring-kafka:${project.ext.springKafkaVersion}"
	annotationProcessor "org.projectlombok:lombok:${project.ext.lombokVersion}"
	compileOnly "org.projectlombok:lombok:${project.ext.lombokVersion}"
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'io.projectreactor.netty:reactor-netty'
	implementation "com.google.protobuf:protobuf-java:${project.ext.protobufVersion}"
	implementation "com.google.protobuf:protobuf-java-util:${project.ext.protobufVersion}"
}

jib {
	container {
		ports = ['8080']
		jvmFlags = ["-Dspring.profiles.active=${System.env.PROFILE}"]
		environment = [
				'VAULT_TOKEN': "${System.env.VAULT_TOKEN}".toString(),
				'TZ'         : 'Europe/Moscow'
		]
		creationTime = 'USE_CURRENT_TIMESTAMP'
		labels = [
				'maintainer': 'Roman Nagovitcyn @romketa',
				'version'   : "${project.version}".toString()
		]
	}
	from {
		image = "${project.ext.dockerImage}"
		platforms {
			platform {
				architecture = "${project.ext.dockerArch}"
				os = 'linux'
			}
		}
	}
	to {
		image = "${project.ext.dockerHubName}/${project.name}-${System.env.PROFILE}"
		tags = ['latest', "${project.version}"]
	}
}

tasks.jib.dependsOn test
tasks.jibDockerBuild.dependsOn test

tasks.named('bootJar') {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register('printVersion') {
	doLast {
		println project.version
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

